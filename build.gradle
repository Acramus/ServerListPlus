/*
 *        _____                     __    _     _   _____ _
 *       |   __|___ ___ _ _ ___ ___|  |  |_|___| |_|  _  | |_ _ ___
 *       |__   | -_|  _| | | -_|  _|  |__| |_ -|  _|   __| | | |_ -|
 *       |_____|___|_|  \_/|___|_| |_____|_|___|_| |__|  |_|___|___|
 *
 *  ServerListPlus - http://git.io/slp
 *    > The most customizable server status ping plugin for Minecraft!
 *  Copyright (c) 2014, Minecrell <https://github.com/Minecrell>
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

allprojects {
    version = '3.4-SNAPSHOT'

    apply plugin: 'java'
    sourceCompatibility = 1.7 // TODO: Update to Java 8 in the future
    targetCompatibility = 1.7

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    repositories {
        mavenCentral()
        maven { url 'http://repo.minecrell.net/releases' }
    }
}

ext {
    // Placeholders for configuration filtering
    description = 'The most customizable server status ping plugin for Minecraft!'
    author = 'Minecrell <https://github.com/Minecrell>'

    javaPackage = 'net.minecrell.serverlistplus'
    resourceTokens = [
            'Name': rootProject.name,
            'Description': description,
            'Version': version,
            'Author': author,
            'Website': 'http://git.io/slp',
            'Wiki': 'http://git.io/slp-wiki'
    ];

    universalProjects = [
            ':Bukkit', ':Bungee', ':Canary', ':Sponge'
    ]
}

subprojects {
    dependencies {
        compile 'org.projectlombok:lombok:1.16.0'
    }

    plugins.withId('com.github.johnrengelman.shadow') {
        shadowJar {
            classifier = ''
            baseName = rootProject.name + project.name
            exclude 'META-INF/'
        }
    }
}

task universal(type: Jar) {
    // TODO: I should probaby improve this...
    baseName = "${rootProject.name}Universal"
    duplicatesStrategy 'exclude' // Skip the second plugin.yml of the Bungee plugin
    universalProjects.each {
        evaluationDependsOn it // Load the projects completely first
        dependsOn project(it).tasks.shadowJar // Now wait to complete the plugin creation
        from zipTree(project(it).shadowJar.archivePath) // And add it to our universal JAR
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

