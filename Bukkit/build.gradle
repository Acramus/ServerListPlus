/*
 *        _____                     __    _     _   _____ _
 *       |   __|___ ___ _ _ ___ ___|  |  |_|___| |_|  _  | |_ _ ___
 *       |__   | -_|  _| | | -_|  _|  |__| |_ -|  _|   __| | | |_ -|
 *       |_____|___|_|  \_/|___|_| |_____|_|___|_| |__|  |_|___|___|
 *
 *  ServerListPlus - http://git.io/slp
 *  Copyright (c) 2014, Minecrell <https://github.com/Minecrell>
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins { id 'com.github.johnrengelman.shadow' version '1.2.2' }

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    maven { url 'http://repo.dmulloy2.net/content/repositories/releases' }
    maven { url 'http://repo.dmulloy2.net/content/repositories/snapshots' }
    maven { url 'http://repo.mcstats.org/content/repositories/releases' }
}

ext {
    // TODO: Better fix?
    resourceTokens = resourceTokens.clone()
    pluginPackage = "${javaPackage}.bukkit"
    pluginClass = "${pluginPackage}.BukkitPlugin"
    resourceTokens.PluginClass = pluginClass.toString()
}

dependencies {
    compile project(':Core')
    compile 'org.bukkit:bukkit:1.8.8-R0.1-SNAPSHOT',
            'com.comphenix.protocol:ProtocolLib:3.6.4'
    compile('org.mcstats.bukkit:metrics-lite:R7') {
        exclude module: 'bukkit'
    }
}

processResources {
    include 'plugin.yml'
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: resourceTokens
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task v1_7(type: ShadowJar, dependsOn: build) {
    from sourceSets.main.output
    classifier = '1.7.X'
    baseName = rootProject.name + project.name
    exclude 'META-INF/'
    configurations = [project.configurations.runtime]

    dependencies {
        include project(':Core')
        include dependency('org.mcstats.bukkit:metrics-lite:R7')
    }

    relocate 'org.mcstats', "${pluginPackage}.metrics"

    relocate "${javaPackage}.core", "${pluginPackage}.core"
    relocate 'com.google.common', 'net.minecraft.util.com.google.common'
    relocate 'com.google.gson', 'net.minecraft.util.com.google.gson'
}

task v1_8(type: ShadowJar, dependsOn: build) {
    from sourceSets.main.output
    classifier = '1.8'
    baseName = rootProject.name + project.name
    exclude 'META-INF/'
    configurations = [project.configurations.runtime]

    dependencies {
        include project(':Core')
        include dependency('org.mcstats.bukkit:metrics-lite:R7')
    }

    relocate 'org.mcstats', "${pluginPackage}.metrics"

    // Spigot 1.8
    relocate "${javaPackage}.core", "${pluginPackage}.core"
    //relocate 'com.google.common', 'net.minecraft.util.com.google.common'
    relocate 'com.google.gson', 'org.bukkit.craftbukkit.libs.com.google.gson'
}

shadowJar {
    dependencies {
        include project(':Core')
        include dependency('org.mcstats.bukkit:metrics-lite:R7')
    }

    relocate 'org.mcstats', "${pluginPackage}.metrics"
}
