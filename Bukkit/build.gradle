/*
 *        _____                     __    _     _   _____ _
 *       |   __|___ ___ _ _ ___ ___|  |  |_|___| |_|  _  | |_ _ ___
 *       |__   | -_|  _| | | -_|  _|  |__| |_ -|  _|   __| | | |_ -|
 *       |_____|___|_|  \_/|___|_| |_____|_|___|_| |__|  |_|___|___|
 *
 *  ServerListPlus - Customize your server list ping!
 *  Copyright (C) 2014, Minecrell <https://github.com/Minecrell>
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

repositories {
    maven { url 'http://repo.bukkit.org/content/groups/public' }
    maven { url 'http://repo.comphenix.net/content/groups/public' }
    maven { url 'http://repo.mcstats.org/content/repositories/public' }
}

apply from: rootProject.file('gradle/shadow.gradle')

ext {
    // TODO: Better fix?
    resourceTokens = new LinkedHashMap<String, String>(resourceTokens)
    pluginPackage = javaPackage + '.bukkit'
}

dependencies {
    compile project(':Core')
    compile 'org.bukkit:bukkit:1.7.9-R0.2',
            'com.comphenix.protocol:ProtocolLib:3.3.1',
            'org.mcstats.bukkit:metrics-lite:R7'
}

resourceTokens.put 'PluginClass', pluginPackage + '.BukkitPlugin'

processResources {
    include 'plugin.yml'
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: resourceTokens
}

shadow {
    artifactSet {
        include 'ServerListPlus:Core'
        include 'org.mcstats.bukkit:metrics-lite'
    }

    relocation { // TODO: Replace with variable
        pattern = 'net.minecrell.serverlistplus.core'
        shadedPattern = 'net.minecrell.serverlistplus.bukkit.core'
    }

    relocation {
        pattern = 'org.mcstats'
        shadedPattern = 'net.minecrell.serverlistplus.bukkit.metrics'
    }

    // TODO: Use the libraries included in Minecraft for now
    relocation {
        pattern = 'com.google.common'
        shadedPattern = 'net.minecraft.util.com.google.common'
    }
    relocation {
        pattern = 'com.google.gson'
        shadedPattern = 'net.minecraft.util.com.google.gson'
    }
}