/*
 *        _____                     __    _     _   _____ _
 *       |   __|___ ___ _ _ ___ ___|  |  |_|___| |_|  _  | |_ _ ___
 *       |__   | -_|  _| | | -_|  _|  |__| |_ -|  _|   __| | | |_ -|
 *       |_____|___|_|  \_/|___|_| |_____|_|___|_| |__|  |_|___|___|
 *
 *  ServerListPlus - http://git.io/slp
 *    > The most customizable server status ping plugin for Minecraft!
 *  Copyright (c) 2014, Minecrell <https://github.com/Minecrell>
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins { id 'com.github.johnrengelman.shadow' version '1.1.2' }

repositories {
    maven { url 'http://repo.md-5.net/content/groups/public' }
    maven { url 'http://repo.comphenix.net/content/groups/public' }
    maven { url 'http://repo.mcstats.org/content/repositories/public' }
}

ext {
    // TODO: Better fix?
    resourceTokens = resourceTokens.clone()
    pluginPackage = "${javaPackage}.bukkit"
    pluginClass = "${pluginPackage}.BukkitPlugin"
    resourceTokens.PluginClass = pluginClass.toString()
}

dependencies {
    compile project(':Core')
    compile 'com.google.guava:guava:15.0', // TODO: Better fix for guava compilation in Bukkit
            'org.spigotmc:spigot-api:1.7.10-R0.1-SNAPSHOT',
            'com.comphenix.protocol:ProtocolLib:3.4.0',
    compile('org.mcstats.bukkit:metrics-lite:R7') {
        exclude module: 'bukkit'
    }
}

processResources {
    include 'plugin.yml'
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: resourceTokens
}

shadowJar {
    dependencies {
        include project(':Core')
        include dependency('org.mcstats.bukkit:metrics-lite:R7')
    }

    relocate "${javaPackage}.core", "${pluginPackage}.core"
    relocate 'org.mcstats', "${pluginPackage}.metrics"

    // TODO: Use the libraries included in Minecraft for now
    relocate 'com.google.common', 'net.minecraft.util.com.google.common'
    relocate 'com.google.gson', 'net.minecraft.util.com.google.gson'
}